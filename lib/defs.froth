; defs.froth - Definition utilities
; Definition utilities for creating closures with minimal environments

; restrict-env-impl: (old-env keys result idx self -- new-env)
; Implementation helper for restrict-env.
{ /self /idx /result /keys /old-env
  idx keys # = {
    result
  } {
    keys idx @ /key
    old-env key in {
      ; key in env (0 = found), add to result
      old-env keys result old-env key @ key : idx 1 + self self!
    } {
      ; key not in env, skip
      old-env keys result idx 1 + self self!
    } ?!
  } ?!
} /restrict-env-impl

; restrict-env: (old-env keys -- new-env)
; Restrict env map to only the keys in the array.
; Note: Can't use stdlib's restrict since this is imported first.
{ /keys /old-env
  old-env keys $ 0 restrict-env-impl restrict-env-impl!
} /restrict-env

; def-fn: (deps closure -- closure)
; Create a closure with its environment restricted to just the specified
; dependencies. This prevents environment bloat from propagating through
; the definition chain.
;
; Example:
;   { 1 } /a
;   { a! 1 + } /b
;   ['b] { b! 1 + } def-fn! /c   ; c's env contains only b, not a
{ /closure /deps
  closure open /body /env
  env deps restrict-env! body close
} /def-fn

; Remove impl functions from the environment
env 'restrict-env-impl delete 'restrict-env delete restore
