; lib/map.froth
; Map utilities

; restrict: (map keys -- map)
; Restrict map to only the keys in the array.
; Ignores array elements that are not quoted identifiers.
['reduce] { /keys /m
  keys $ {
    /k /result
    k isIdent {
      m k in {
        result m k @ k :
      } {
        result
      } ?!
    } {
      result
    } ?!
  } reduce!
} def-fn! /restrict

; transform-values: (map func -- map)
; Transform each value in the map using the function.
['reduce] { /f /m
  m keys $ {
    /k /result
    m k @ f! /new-val
    result new-val k :
  } reduce!
} def-fn! /transform-values

; merge: (map1 map2 -- map)
; Merge two maps, with map2 values taking precedence.
;
; Example:
;   $ 1 'a : 2 'b : $ 3 'b : 4 'c : merge!
;   ; produces $ 1 'a : 3 'b : 4 'c :
['foldl] { /m2 /m1
  m1 m2 keys { /k m2 k @ k : } foldl!
} def-fn! /merge

; delete-keys: (map keys -- map)
; Delete an array of keys from a map.
;
; Example:
;   $ 1 'a : 2 'b : 3 'c : ['a 'c] delete-keys!
;   ; produces $ 2 'b :
['reduce] { /ks /m
  ks m { /k /acc acc k delete } reduce!
} def-fn! /delete-keys
