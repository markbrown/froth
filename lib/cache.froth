; cache.froth - Two-level cache for closure compilation
;
; Maps identifiers to alists of (closureval, bytecodeval) pairs.
; Used to avoid recompiling the same closure multiple times.
;
; Structure:
;   Outer: map from quoted identifiers to alists
;   Inner: alist mapping closureval -> bytecodeval

; cache-empty: (-- cache)
; Create an empty cache.
[] { $ } def-fn! /cache-empty

; cache-get: (cache 'ident closureval -- bytecodeval 0 | 1)
; Look up closureval under identifier. Returns (bytecodeval 0) if found, (1) if not.
['alist-get] { /closureval /ident /cache
  cache ident in {
    ; ident exists, search its alist
    cache ident @ closureval alist-get!
  } {
    ; ident not in cache
    1
  } ?!
} def-fn! /cache-get

; cache-set: (cache 'ident closureval bytecodeval -- cache')
; Store the mapping from closureval to bytecodeval under identifier.
['alist-set] { /bytecodeval /closureval /ident /cache
  cache ident in {
    ; ident exists, update its alist
    cache ident @ /alist
    cache alist closureval bytecodeval alist-set! ident :
  } {
    ; ident not in cache, create new alist
    cache . closureval bytecodeval alist-set! ident :
  } ?!
} def-fn! /cache-set
