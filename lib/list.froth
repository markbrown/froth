; list.froth - Cons list utilities
;
; Lists are built from cons cells:
;   .              ; empty list (nil)
;   . 3 , 2 , 1 ,  ; list [1, 2, 3] (1 is head)
;
; Use fst/snd to access head/tail, isNil to test for empty.

; lfoldl: Apply a function to each element head-to-tail, leaving results on stack
; (list fn -- ...results...)
;
; The function may leave zero or more values on the stack per element.
; Use a generator to collect results into an array.
;
; Examples:
;   . 3 , 2 , 1 , {1 +} lfoldl!        ; leaves 2 3 4 on stack
;   [ . 3 , 2 , 1 , {1 +} lfoldl! ]    ; produces [2 3 4]
;   . 3 , 2 , 1 , {print} lfoldl!      ; prints 1, 2, 3

{/fn /list
  {/self /xs
    xs isNil
      {}
      { xs fst fn!
        xs snd self self! }
    ?!
  } /impl
  list impl impl!
} /lfoldl

; lfoldr: Apply a function to each element tail-to-head, leaving results on stack
; (list fn -- ...results...)
;
; Examples:
;   . 3 , 2 , 1 , {print} lfoldr!      ; prints 3, 2, 1

{/fn /list
  {/self /xs
    xs isNil
      {}
      { xs snd self self!
        xs fst fn! }
    ?!
  } /impl
  list impl impl!
} /lfoldr

; lreverse: Reverse a list
; (list -- list)
;
; Example:
;   . 3 , 2 , 1 , lreverse!  ; produces . 1 , 2 , 3 , (i.e., [3, 2, 1])

{/xs . xs {,} lfoldl!} /lreverse
