; list.froth - Cons list utilities
;
; Lists are built from cons cells:
;   .              ; empty list (nil)
;   . 3 , 2 , 1 ,  ; list [1, 2, 3] (1 is head)
;
; Use fst/snd to access head/tail, isNil to test for empty.

; lfoldl: Apply a function to each element head-to-tail, leaving results on stack
; (list fn -- ...results...)
;
; The function may leave zero or more values on the stack per element.
; Use a generator to collect results into an array.
;
; Examples:
;   . 3 , 2 , 1 , {1 +} lfoldl!        ; leaves 2 3 4 on stack
;   [ . 3 , 2 , 1 , {1 +} lfoldl! ]    ; produces [2 3 4]
;   . 3 , 2 , 1 , {print} lfoldl!      ; prints 1, 2, 3

; lfoldl-impl: (xs fn self -- ...results...)
[] {/self /fn /xs
  xs isNil
    {}
    { xs fst fn!
      xs snd fn self self! }
  ?!
} def-fn! /lfoldl-impl

; lfoldl: (list fn -- ...results...)
['lfoldl-impl] {/fn /list
  list fn lfoldl-impl lfoldl-impl!
} def-fn! /lfoldl

; lfoldr: Apply a function to each element tail-to-head, leaving results on stack
; (list fn -- ...results...)
;
; Examples:
;   . 3 , 2 , 1 , {print} lfoldr!      ; prints 3, 2, 1

; lfoldr-impl: (xs fn self -- ...results...)
[] {/self /fn /xs
  xs isNil
    {}
    { xs snd fn self self!
      xs fst fn! }
  ?!
} def-fn! /lfoldr-impl

; lfoldr: (list fn -- ...results...)
['lfoldr-impl] {/fn /list
  list fn lfoldr-impl lfoldr-impl!
} def-fn! /lfoldr

; Remove impl functions from the environment
env 'lfoldl-impl delete 'lfoldr-impl delete restore

; lreverse: Reverse a list
; (list -- list)
;
; Example:
;   . 3 , 2 , 1 , lreverse!  ; produces . 1 , 2 , 3 , (i.e., [3, 2, 1])

['lfoldl] {/xs . xs {,} lfoldl!} def-fn! /lreverse

; lconcat: Concatenate two lists
; (list1 list2 -- list)
;
; Example:
;   . 2 , 1 , . 4 , 3 , lconcat!  ; produces . 4 , 3 , 2 , 1 , (i.e., [1, 2, 3, 4])

['lreverse 'lfoldl] {/xs2 /xs1
  xs2 xs1 lreverse! {,} lfoldl!
} def-fn! /lconcat

; lmap: Apply function to each element, returning a new list
; (list fn -- list)
;
; Example:
;   . 3 , 2 , 1 , {1 +} lmap!  ; produces . 4 , 3 , 2 , (i.e., [2, 3, 4])

['lfoldr] {/fn /xs
  . xs {fn! ,} lfoldr!
} def-fn! /lmap
